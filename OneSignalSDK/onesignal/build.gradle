apply plugin: 'com.android.library'
android {
    compileSdk 34
    namespace "com.onesignal"
    defaultConfig {
        // Normally we shouldn't need to redefine manifestPlaceholders for our onesignal project
        //   but Android Studio gets a sync error if these are not here.
        //      - /gradlew onesignal:assembleRelease is fine from the console.
        // We can't use static values are these are directly added to the AndroidManifest.xml in the .aar file.
        manifestPlaceholders = [onesignal_app_id: '${onesignal_app_id}',
                                // Project number pulled from dashboard, local value is ignored
                                onesignal_google_project_number: '${onesignal_google_project_number}']
        minSdkVersion 26
        targetSdk 34
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    lintOptions {
        abortOnError false
    }

    buildTypes {
        original {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
        }
        unity {
            minifyEnabled false
        }
    }
    buildFeatures {
        buildConfig = true
    }
}

// api || implementation = compile and runtime

// KEEP: version ranges, these get used in the released POM file for maven central
dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    // play-services-gcm:16.1.0 is the last version before going to AndroidX
    // compileOnly as this is just for fallback code if FCM wasn't added to the project.
    // Remove for 4.0.0 release
    compileOnly 'com.google.android.gms:play-services-gcm:16.1.0'

    // play-services-location:16.0.0 is the last version before going to AndroidX
    // play-services-location:17.0.0 is the first version using AndroidX
    compileOnly 'com.google.android.gms:play-services-location:[17.0.0, 17.99.99]'

    // play-services-ads-identifier:16.0.0 is the last version before going to AndroidX
    // play-services-ads-identifier:17.0.0 is the first version using AndroidX
    // Classes used to live in play-services-base before this
    implementation 'com.google.android.gms:play-services-ads-identifier:[17.0.0, 17.99.99]'

    // play-services-base:16.1.0 is the last version before going to AndroidX
    // play-services-base:17.0.0 is the first version using AndroidX
    // Required for GoogleApiAvailability
    implementation 'com.google.android.gms:play-services-base:[17.0.0, 17.99.99]'

    // firebase-messaging:18.0.0 is the last version before going to AndroidX
    // firebase-messaging:17.6.0 is the max version since we still have code looking for FirebaseInstanceIdService
    api 'com.google.firebase:firebase-messaging:17.6.0'

    // Huawei PushKit
    // KEEP as "compileOnly", so OneSignal isn't a direct dependency in the POM file.
    compileOnly 'com.huawei.hms:push:6.11.0.300'
    compileOnly 'com.huawei.hms:location:6.12.0.300'

    api 'androidx.cardview:cardview:[1.0.0, 1.99.99]'
    api 'androidx.legacy:legacy-support-v4:[1.0.0, 1.99.99]'
    api 'androidx.browser:browser:[1.0.0, 1.99.99]'
    api 'androidx.appcompat:appcompat:[1.0.0, 1.99.99]'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.22"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22"
}

//apply from: 'maven-push.gradle'
